group 'com.capgemini.brahma'
description = """Brahma - A seed-project for spring-boot-camel apps"""

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "http://dl.bintray.com/capgeminiuk/maven/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'spring-boot'

jar {
    baseName = 'camel-boot'
    version =  '0.1.0-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "http://dl.bintray.com/capgeminiuk/maven/"
    }
}

def springBootVersion = '1.3.1.RELEASE'
def camelVersion =      '2.16.2'

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationtest/java')
        }
        resources.srcDir file('src/integrationtest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    compile (group: 'org.apache.camel',         name: 'camel-spring-boot',                  version: camelVersion)
    compile (group: 'org.apache.camel',         name: 'camel-netty4-http',                  version: camelVersion)
    compile (group: 'org.apache.camel',         name: 'camel-jetty9',                       version: camelVersion)
    compile (group: 'org.apache.camel',         name: 'camel-jackson',                      version: camelVersion)
    compile (group: 'org.apache.camel',         name: 'camel-swagger-java',                 version: camelVersion)
    compile (group: 'org.apache.camel',         name: 'camel-cxf',                          version: camelVersion)
    compile (group: 'org.springframework.boot', name: 'spring-boot',                        version: springBootVersion)
    compile (group: 'org.springframework.boot', name: 'spring-boot-starter-web',            version: springBootVersion)
    compile (group: 'org.springframework.boot', name: 'spring-boot-starter-jetty',          version: springBootVersion)
    compile (group: 'com.netflix.hystrix',      name: 'hystrix-metrics-event-stream',       version: '1.3.16')
    compile (group: 'com.capgemini',            name: 'codahale-metrics-filters',           version: '0.11.0')
    compile (group: 'com.bealetech',            name: 'metrics-statsd',                     version: '3.0.2')
    compile (group: 'com.capgemini',            name: 'springboot-camel-metrics-publisher', version: '0.11.0')
    compile (group: 'com.capgemini',            name: 'archaius-spring-adapter',            version: '0.9.6')

    testCompile (group: 'junit',                    name: 'junit',                    version: '4.11')
    testCompile (group: 'com.jayway.restassured',   name: 'rest-assured',             version: '2.4.1') // add for all web-based testing
    testCompile (group: 'org.apache.camel',         name: 'camel-test-spring',        version: camelVersion)
    testCompile (group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion)
    testCompile (group: 'javax.servlet',            name: 'javax.servlet-api',        version: '3.1.0') // TODO: this is a duplicate
}

configurations {
    compile.exclude(module: 'spring-boot-starter-tomcat')
}

springBoot {
    mainClass = 'org.apache.camel.spring.boot.FatJarRouter'
}
